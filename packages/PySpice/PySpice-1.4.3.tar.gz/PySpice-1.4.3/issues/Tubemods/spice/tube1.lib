* TUBE LIBRARY TUBE1.LIB:  THIS LIBRARY CONTAINS SOME POWER TRIODES, ETC.
* THAT I DON'T USE.  I HAVE CREATED THIS FILE BECAUSE THE EVALUATION VERSION
* OF PSPICE LIMITS USER-LIBRARIES TO 20 DEVICES.  YOU MAY WISH TO MOVE SOME
* MODELS HERE INTO TUBE.LIB.

.SUBCKT 6550 1 2 3 4 ;  P G1 C G2
+ PARAMS: MU=7.9 EX=1.35 KG1=890 KG2=4200 KP=60 KVB=24
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE=  ; E1 BREAKS UP LONG EQUATION FOR G1.
+{V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
G2     4 3  VALUE={(EXP(EX*(LOG((V(4,3)/MU)+V(2,3)))))/KG2}
* G2   4 3  VALUE={PWR(V(4,3)/MU+V(2,3),EX)/KG2}
RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}   ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT EL34 1 2 3 4 ;  P G1 C G2
+ PARAMS: MU=11 EX=1.35 KG1=650 KG2=4200 KP=60 KVB=24
+         CCG=15P CPG1=1P CCP=8P RGI=1K 
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE=  ; E1 BREAKS UP LONG EQUATION FOR G1.
+{V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
G2     4 3  VALUE={(EXP(EX*(LOG((V(4,3)/MU)+V(2,3)))))/KG2}
RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}   ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 12AX7 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=100 EX=1.4 KG1=1060 KP=600 KVB=300 RGI=2000 
+ CCG=2.3P  CGP=2.4P CCP=.9P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID
C2 2 1 {CGP}  ; GRID=PLATE
C3 1 3 {CCP}  ; CATHODE-PLATE
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 12AU7 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=21.5 EX=1.3 KG1=1180 KP=84 KVB=300 RGI=2000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 6AN8T 1 2 3  ; P G C; NEW MODEL   ; TRIODE SECTION
+ PARAMS: MU=21.5 EX=1.3 KG1=1180 KP=84 KVB=300 RGI=2000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 6AN8P 1 2 3 4 ;  P G1 C G2  ; PENTODE SECTION
+ PARAMS: MU=11 EX=1.35 KG1=650 KG2=4200 KP=60 KVB=24
+         CCG=15P CPG1=1P CCP=8P RGI=1K 
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE=  ; E1 BREAKS UP LONG EQUATION FOR G1.
+{V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
G2     4 3  VALUE={(EXP(EX*(LOG((V(4,3)/MU)+V(2,3)))))/KG2}
RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}   ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 2A3 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=4.2 EX=1.4 KG1=1500 KP=60 KVB=300 RGI=2000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 300B 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=3.95 EX=1.4 KG1=1550 KP=65 KVB=300 RGI=1000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 6C33C 1 2 3  ; P G C; TWO CATHODES FROM BORBELY, GA 5/96.
+ PARAMS: MU=3.1 EX=1.4 KG1=163 KP=15 KVB=300 RGI=1000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 12AT7 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=60 EX=1.35 KG1=460 KP=300 KVB=300 RGI=2000
+ CCG=2.3P  CGP=2.2P CCP=1.0P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 6DJ8 1 2 3  ; P G C; NEW MODEL
+ PARAMS: MU=28 EX=1.3 KG1=330 KP=320 KVB=300 RGI=2000
+ CCG=2.3P  CGP=2.1P CCP=.7P  ; ADD .7PF TO ADJACENT PINS; .5 TO OTHERS. 
E1 7 0 VALUE=
+{V(1,3)/KP*LOG(1+EXP(KP*(1/MU+V(2,3)/SQRT(KVB+V(1,3)*V(1,3)))))}
RE1 7 0 1G
G1 1 3 VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1}
RCP 1 3 1G    ; TO AVOID FLOATING NODES IN MU-FOLLOWER
C1 2 3 {CCG}  ; CATHODE-GRID; WAS 1.6P
C2 2 1 {CGP}  ; GRID-PLATE; WAS 1.5P
C3 1 3 {CCP}  ; CATHODE-PLATE; WAS 0.5P
D3 5 3 DX     ; FOR GRID CURRENT
R1 2 5 {RGI}  ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT 6L6GC 1 2 3 4 ;  P G1 C G2
+ PARAMS: MU=8.7 EX=1.35 KG1=1460 KG2=4500 KP=48 KVB=12
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE=  ; E1 BREAKS UP LONG EQUATION FOR G1.
+{V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
G2   4 3  VALUE={(EXP(EX*(LOG((V(4,3)/MU)+V(2,3)))))/KG2}
RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}    ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT KT88 1 2 3 4 ;  P G1 C G2
+ PARAMS: MU=8.8 EX=1.35 KG1=730 KG2=4800 KP=32 KVB=16
+         CCG=14P CPG1=.85P CCP=12P RGI=1K 
RE1  7 0  1MEG    ; DUMMY SO NODE 7 HAS 2 CONNECTIONS
E1   7 0  VALUE=  ; E1 BREAKS UP LONG EQUATION FOR G1.
+{V(4,3)/KP*LOG(1+EXP((1/MU+V(2,3)/V(4,3))*KP))}
G1   1 3  VALUE={(PWR(V(7),EX)+PWRS(V(7),EX))/KG1*ATAN(V(1,3)/KVB)}
G2   4 3  VALUE={(EXP(EX*(LOG((V(4,3)/MU)+V(2,3)))))/KG2}
RCP  1 3  1G      ; FOR CONVERGENCE
C1   2 3  {CCG}    ; CATHODE-GRID 1
C2   1 2  {CPG1}  ; GRID 1-PLATE
C3   1 3  {CCP}   ; CATHODE-PLATE
R1   2 5  {RGI}   ; FOR GRID CURRENT
D3   5 3  DX      ; FOR GRID CURRENT
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
.ENDS

.SUBCKT PAT-4006-CFB 1 2 3 4 5 6 7 8 9 10 11
* PLITRON PAT-4006-CFB OUTPUT TRANSFORMER 2KOHM UL PRIMARY
* OL NUMBERS CORRESPOND TO TRANSFORMER SCHEMATIC.
LP1 1 2   35.325H     ; PRIMARY
LP2 2 3   15.7H
LP3 3 4   15.7H
LP4 4 5   35.325H   
CP1 1 5   .342NF      ; CAPACITANCE FROM SPECS
LP5 8 7   .2453125H   ; 1/2 SPEAKER SECONDARY
LP6 7 6   .2493125H   ; " "
LP7 11 10  .98125H     ; 1/2 FBK WINDING
LP8 10 9 .98125H     ; " "
KALL LP1 LP2 LP3 LP4 LP5 LP6 LP7 LP8 .9999987 ; 1-1/(2*403600) AWESOME!
.ENDS

.SUBCKT DYNA_OUTPUT_XFRMR 1 2 3 4 5 6 7 8 9
* DYNACO OUTPUT XFRMR WITH 4300 OHM UL PRIMARY
* BASED ON 4/25/96 MEASUREMENTS (5/28/96 CALCUALTIONS)
* TOTAL PRIMARY L=7.96Hy.
LP1 1 2   .7164H     ; PRIMARY
LP2 2 3   .3184H
LP3 3 4   .3184H
LP4 4 5   .7164H   
CP1 1 5   3.183NF   ; CAPACITANCE (MEASURED)
LP5 7 6   2.543mH   ; 8-16  OHM WINDING
LP6 8 7   1.269mH   ; 4-8   OHM WINDING
LP7 9 8   7.405mH   ; COM-4 OHM WINDING
KALL LP1 LP2 LP3 LP4 LP5 LP6 LP7 .9999875 ; = 1-1/(2*40000): OK.
.ENDS

*model D1N5292  anode
*               | cathode
*               | |
.subckt D1N5292 1 2
  j1 1 2 2 jx
  .model   jx   NJF(Beta=134u Betatce=-.5 Rd=1 Rs=0 Lambda=366.1u Vto=-2.149
+               Vtotc=-2.5m N=2 Xti=3 Cgd=3p Is=10f M=.5 Pb=1 Fc=.5 Af=1)
*               Motorola        pid=NKL         case=DO7
*               88-07-25 bam    POVmax=100
*               88-12-16 pwt
.ends

*model D1N5295  anode
*               | cathode
*               | |
.subckt D1N5295 1 2
  j1 1 2 2 jx
  .model   jx   NJF(Beta=277.5u Betatce=-.5 Rd=1 Rs=0 Lambda=203.3u Vto=-1.721
+               Vtotc=-2.5m N=2 Xti=3 Cgd=4.5p Is=10f M=.5 Pb=1 Fc=.5 Af=1)
*               Motorola        pid=NKM         case=DO7
*               88-07-25 bam    POVmax=100
*               88-12-16 pwt
.ends

*  .PARAM statements:  The parameter to be stepped
*  in the PSPICE schematic is PAR1.  (PAR1 is a global variable set in
*  Analysis...  Setup...   Parametric... in the PSPICE schematic program.) 

* .PARAM PAR1=1.                        ; NORMAL SETTING IF NOT STEPPED.
* .PARAM PARM={1-PAR1}
* .STEP PARAM PFRAC 0 1 .25  ; -1 1 1 ;  0 1 .25
* .PARAM PARP={ .5*(PAR1+ABS(PAR1))}    ; =  PAR1  IF PAR1 >= 0
* .PARAM PARN={-.5*(PAR1-ABS(PAR1))}    ; = |PAR1| IF PAR1 <= 0
*  PARTX IS FOR THE PAS TREBLE CONTROL.
* .PARAM PAR2={.5*(1-COS(3.141593*PAR1))}
* .PARAM PARTX={(EXP(4.60517*PAR1)-1)/99}
